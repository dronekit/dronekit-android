apply plugin: 'com.android.library'

ext {
    PUBLISH_ARTIFACT_ID = 'dronekit-android'
    PUBLISH_VERSION = '2.5.5'
    PROJECT_DESCRIPTION = "Android DroneKit client library."
    PROJECT_LABELS = ['3DR', '3DR Services', 'DroneAPI', 'Android', 'DroneKit']
    PROJECT_LICENSES = ['Apache-2.0']
}
/* Every time something is changed in the client library the versionCode and
 PUBLISH_VERSION must be incremented.
 */
android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        versionCode 20505
        versionName PUBLISH_VERSION
    }

    sourceSets {
        main {
            aidl.srcDirs = ['src/main/java']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }

    android.libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            if (file != null && file.name.endsWith('.aar')) {
                output.outputFile = new File(file.parent, "${PUBLISH_ARTIFACT_ID}.${PUBLISH_VERSION}.aar")
            }
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:22.1.1'
    compile 'com.google.android.gms:play-services-base:7.3.0'

    compile project(':dependencyLibs:Mavlink')
    compile files('libs/Mavlink.jar')
}

assemble.dependsOn ':dependencyLibs:Mavlink:jar'

apply from: "${rootDir}/release_android_library.gradle"