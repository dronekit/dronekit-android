apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

dependencies {

    compile "com.google.android.gms:play-services-analytics:${play_services_version}"
    compile "com.google.android.gms:play-services-location:${play_services_version}"

    compile "com.android.support:support-v4:${support_lib_version}"
    compile "com.android.support:appcompat-v7:${support_lib_version}"
    compile "com.android.support:cardview-v7:${support_lib_version}"
    compile "com.android.support:recyclerview-v7:${support_lib_version}"

    compile files('libs/d2xx.jar')

    compile project(':ClientLib')
    compile project(':UsbSerialLibrary')
    compile('com.crashlytics.sdk.android:crashlytics:2.2.2@aar') {
        transitive = true;
    }

    compile 'com.jakewharton.timber:timber:3.1.0'
    compile 'com.jcraft:jsch:0.1.51'

    compile "com.squareup.okhttp:okhttp:${okhttp_version}"
    compile "com.squareup.okhttp:okhttp-urlconnection:${okhttp_version}"

    //Java semver library
    compile 'com.github.zafarkhaja:java-semver:0.9.0'

    //MP4 generation library
    compile 'com.googlecode.mp4parser:isoparser:1.1.7'

    testCompile 'junit:junit:4.12'
    testCompile "org.robolectric:robolectric:3.0"
}

//Decomposed version name and code (https://plus.google.com/+JakeWharton/posts/6f5TcVPRZij)
def versionMajor = 1
def versionMinor = 5
def versionPatch = 1
def versionBuild = 6 //bump for dogfood builds, public betas, etc.
def versionPrefix = "3DR Services v"

//Log levels values
def logLevelVerbose = 2;
def logLevelDebug = 3;
def logLevelInfo = 4;
def logLevelWarn = 5;
def logLevelError = 6;
def logLevelAssert = 7;

android {
    compileSdkVersion android_build_sdk_version
    buildToolsVersion android_build_tools_version

    defaultConfig {
        applicationId 'org.droidplanner.services.android'
        minSdkVersion android_build_min_sdk_version
        targetSdkVersion android_build_target_sdk_version
        buildConfigField "boolean", "SITL_DEBUG", "false"
        buildConfigField "String", "SOLO_LINK_IP", getSoloLinkIp()

        versionCode computeVersionCode(versionMajor, versionMinor, versionPatch, versionBuild)
        versionName generateVersionName(versionPrefix, versionMajor, versionMinor, versionPatch)

        //Write logs to file preferences
        buildConfigField "boolean", "WRITE_LOG_FILE", "false"
        buildConfigField "int", "LOG_FILE_LEVEL", "$logLevelDebug"
        buildConfigField "boolean", "ENABLE_CRASHLYTICS", "false"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        test {
            java.srcDirs = ['test/java']
        }

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    signingConfigs {
        release {
            storeFile getAppKeystoreFile()
            storePassword getAppKeystorePassword()
            keyAlias getAppKey()
            keyPassword getAppKeyPassword()
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            if (file != null && file.name.endsWith('.apk')) {
                def buildType = variant.buildType.getName()
                def versionCode = defaultConfig.versionCode
                output.outputFile = new File(file.parent, "3dr-services-${buildType}.${versionCode}.apk")
            }
        }
    }

    buildTypes {
        debug {
            versionNameSuffix generateVersionNameSuffix(versionBuild, "dev", true)
            buildConfigField "boolean", "WRITE_LOG_FILE", "true"
        }

        beta {
            signingConfig signingConfigs.release
            versionNameSuffix generateVersionNameSuffix(versionBuild, "beta")
            buildConfigField "boolean", "WRITE_LOG_FILE", "true"
            buildConfigField "boolean", "ENABLE_CRASHLYTICS", "true"
        }

        alpha {
            signingConfig signingConfigs.release
            versionNameSuffix generateVersionNameSuffix(versionBuild, "alpha")
            buildConfigField "boolean", "WRITE_LOG_FILE", "true"
            buildConfigField "boolean", "ENABLE_CRASHLYTICS", "true"
        }

        sitl {
            signingConfig signingConfigs.debug
            versionNameSuffix generateVersionNameSuffix(versionBuild, "sitl")
            buildConfigField "String", "SOLO_LINK_IP", getLocalIp()
            buildConfigField "boolean", "SITL_DEBUG", "true"
            buildConfigField "boolean", "WRITE_LOG_FILE", "true"
            debuggable true
        }

        release {
            signingConfig signingConfigs.release
            buildConfigField "boolean", "ENABLE_CRASHLYTICS", "true"
        }
    }

    //FIXME: Remove when lint errors are taken care of.
    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

def getAppKeystoreFile() {
    def filePath = hasProperty('COM_O3DR_SERVICES_KEYSTORE') ? COM_O3DR_SERVICES_KEYSTORE : null
    return filePath ? file(filePath) : null
}

def getAppKeystorePassword(){
    return hasProperty('COM_O3DR_SERVICES_KEYSTORE_PWD') ? COM_O3DR_SERVICES_KEYSTORE_PWD : ''
}

def getAppKeyPassword() {
    return hasProperty('COM_O3DR_SERVICES_KEY_PWD') ? COM_O3DR_SERVICES_KEY_PWD : ''
}

def getAppKey() {
    return hasProperty('COM_O3DR_SERVICES_KEY_ALIAS') ? COM_O3DR_SERVICES_KEY_ALIAS : ''
}

def getSoloLinkIp() {
    return '\"10.1.1.10\"'
}

def getLocalIp() {
    try {
        return '\"' + InetAddress.getLocalHost().getCanonicalHostName() + '\"'
    }catch(Exception e){
        return '\"\"'
    }
}
